apply plugin: 'maven-publish'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    publishing {
        publications {
            mavenAar(MavenPublication) {
                groupId "com.cashfree.subscription"
                artifactId "coresdk"
                version Versions.apiVersionName

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
            }
            mavenAar(MavenPublication) {
                pom {
                    description = "Cashfree Subscription Android SDK"
                    withXml {
                        Node pomNode = asNode()
                        pomNode.dependencies.'*'.each() {
                            it.parent().remove(it)
                        }
                        pomNode.remove(pomNode.dependencies)
                        def dependenciesNode = asNode().appendNode('dependencies')
                        configurations.api.allDependencies.each {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            dependencyNode.appendNode('scope', "compile")
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                name = "AwsS3"
                Properties properties = new Properties()
                if (project.rootProject.file('local.properties').canRead()) {
                    properties.load(project.rootProject.file("local.properties").newDataInputStream())
                }

                url "s3://maven.cashfree.com/" + properties.getProperty('FLAVOR', '')
                credentials(AwsCredentials) {
                    accessKey properties.getProperty('AWS_ACCESS_KEY', '')
                    secretKey properties.getProperty('AWS_SECRET_KEY', '')
                }
            }
        }
    }
}